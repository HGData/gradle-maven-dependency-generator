plugins {
    id 'groovy'
    id 'maven'
    id 'com.jfrog.artifactory' version '3.1.0'
    id 'com.gradle.plugin-publish' version '0.9.1'
    id 'org.sonarqube' version '2.5'
}

apply plugin: 'com.hgdata.gradle.plugin.maven.dependency.generator'

buildscript {
    repositories {
        mavenLocal()
    }
    dependencies {
        classpath group: 'org.hgdata', name: 'gradle-maven-dependency-generator', version: '0.1.0-SNAPSHOT'
    }
}


group = 'com.hgdata'
version = '0.1.0-SNAPSHOT'
sourceCompatibility = 1.8
targetCompatibility = 1.8

defaultTasks 'clean', 'jar', 'generatePomFileForMavenJavaPublication'

apply from: file('gradle/publish.gradle')

dependencies {
    compile gradleApi()
    compile localGroovy()
    compile group: 'com.thoughtworks.xstream', name: 'xstream', version: '1.4.7'
}

jar {
    manifest {
        attributes 'Implementation-Title': 'Gradle Maven Dependency Generator plugin',
                'Implementation-Version': version,
                'Built-By': System.getProperty('user.name'),
                'Built-Date': new Date(),
                'Built-JDK': System.getProperty('java.version'),
                'Built-Gradle': gradle.gradleVersion
    }
}

artifactory {
    contextUrl = "${artifactory_contextUrl}"   //The base Artifactory URL if not overridden by the publisher/resolver
    publish {
        repository {
            repoKey = version.endsWith('SNAPSHOT') ? 'libs-snapshot-local' : 'libs-release-local'
            // The Artifactory repository key to publish to
            username = "${artifactory_user}" // The publisher user name
            password = "${artifactory_password}" // The publisher password
        }
        defaults {
            // Reference to Gradle publications defined in the build script.
            // This is how we tell the Artifactory Plugin which artifacts should be
            // published to Artifactory.
            publications('mavenJava')
            publishArtifacts = true
            // Properties to be attached to the published artifacts.
            properties = ['dev.team': 'packaging']
        }
    }
    resolve {
        repoKey = 'libs-release'
        username = "${artifactory_user}" // The publisher user name
        password = "${artifactory_password}" // The publisher password
    }
}